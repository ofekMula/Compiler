/*************/
/* Based on a template by Oren Ish-Shalom */
/*************/


/*************/
/* IMPORTS */
/*************/

import java_cup.runtime.*;
import ast.*;
import java.util.List;
import java.util.LinkedList;

parser code
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}

	// Override default error reporting in parser
	public void report_error(String message, Object info)
	{
	    System.out.print("ERROR >> ");
        System.out.print("[");
        System.out.print(lexer.getLine());
        System.out.print(":");
        System.out.print(lexer.getCharPos());
        System.out.print("] ");
	    System.err.print("Syntax error at line " + lexer.getLine() + " of input. Parser\n");
		System.exit(1);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
    try {
	s = lexer.next_token();
	System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getCharPos());
	System.out.print("] ");
	System.out.print(sym.terminalNames[s.sym]);
	if (s.value != null)
	 {
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	 }
	System.out.print("\n");
	return s;
     } catch (java.lang.Error e) {
         System.err.print("Syntax error at line " + lexer.getLine() + " of input. Lexer\n");
         System.exit(1);
         return null;
     }
:};

/*************/
/* TERMINALS */
/*************/
terminal PUBLIC, CLASS, MAIN;
terminal EXTENDS ,STATIC, VOID;
terminal PLUS, MINUS, MULT, LT; // DIV ;
terminal LPAREN, RPAREN, L_CUR_PAREN, R_CUR_PAREN;
terminal int INTEGER;
terminal String IDENTIFIER;
terminal FALSE, TRUE;
terminal COMMA, END_SENT, ASSIGN, DOT;
terminal NOT,AND;
terminal INT, BOOLEAN;
terminal NEW;
terminal L_ARRAY, R_ARRAY;
terminal IF, ELSE, WHILE;
terminal LENGTH;
terminal RETURN,THIS;
terminal PRINT, STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal Program prog;
non terminal MainClass mainClass;
non terminal ClassDecl classDecl;
non terminal VarDecl varDecl;
non terminal MethodDecl methodDecl;
non terminal FormalArg formalArg;
non terminal LinkedList<ClassDecl> classDeclsList;
non terminal LinkedList<VarDecl> varDeclsList;
non terminal LinkedList<MethodDecl> methodDeclsList;
non terminal LinkedList<FormalArg> formalsList;
non terminal LinkedList<FormalArg> methodFormalList;
non terminal LinkedList<Expr> methodCallList;
non terminal String Identifier;
non terminal AstType type;
non terminal Statement statement;
non terminal LinkedList<Statement> statementList;

non terminal Expr arrAccessExpr;
non terminal Expr arrLengthExpr;

non terminal Expr expr;
non terminal LinkedList<Expr> exprList;
/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence nonassoc ASSIGN;
precedence nonassoc AND;
precedence left LT;
precedence left PLUS, MINUS;
precedence left MULT;
precedence right NOT, NEW;
precedence left DOT;



/************************/
/* INITIAL NON TERMINAL */
/************************/
start with prog;

/********************/
/* PRODUCTION RULES */
/********************/
type		::=		INT L_ARRAY R_ARRAY {: RESULT = new IntArrayAstType(); :}
            |	BOOLEAN     {: RESULT = new BoolAstType(); :}
            |	INT         {: RESULT = new IntAstType(); :}
            |	Identifier:id       {: RESULT = new RefType(id); :}  ;

prog        ::=  mainClass:main classDeclsList:classList   {:RESULT = new Program(main,classList); :};


mainClass   ::= CLASS Identifier:main L_CUR_PAREN PUBLIC STATIC VOID MAIN LPAREN
		STRING L_ARRAY R_ARRAY Identifier:args RPAREN L_CUR_PAREN statement:mainStatement R_CUR_PAREN R_CUR_PAREN
		{:RESULT = new MainClass(main,args,mainStatement); :} ;

classDecl  ::= CLASS Identifier:className EXTENDS Identifier:superName L_CUR_PAREN varDeclsList:varDecls methodDeclsList:methodDecls
            R_CUR_PAREN     {: RESULT = new ClassDecl(className, superName, varDecls, methodDecls); :}
            | CLASS Identifier:className L_CUR_PAREN varDeclsList:varDecls methodDeclsList:methodDecls R_CUR_PAREN
            {: RESULT = new ClassDecl(className, null, varDecls, methodDecls); :}  ;

classDeclsList  ::= classDecl:classDecl1 classDeclsList:classList {: classList.addFirst(classDecl1);
                                                                    RESULT = classList; :}
               		 | /* empty */           	{: RESULT = new LinkedList<ClassDecl>(); :}  ;

methodDecl  ::= PUBLIC type:returnType Identifier:name methodFormalList:formals L_CUR_PAREN varDeclsList:vardecls statementList:body RETURN expr:retExpr END_SENT R_CUR_PAREN
            {: RESULT = new MethodDecl(returnType, name, formals, vardecls, body, retExpr, 0); :} ;

methodDeclsList ::=  methodDecl:methodDecl1 methodDeclsList:methodList {: methodList.addFirst(methodDecl1);
                                                                                        RESULT = methodList; :}
                                   		 | /* empty */           	{: RESULT = new LinkedList<MethodDecl>(); :}  ;

formalArg ::= type:formalType Identifier:formalName     {: RESULT = new FormalArg(formalType, formalName, 0); :} ;

formalsList  ::= formalArg:formalArg1 COMMA formalsList:formals {: formals.addFirst(formalArg1);
                                                                    RESULT = formals; :}
               		 | formalArg:formalArg1          	{: RESULT = new LinkedList<FormalArg>(); RESULT.add(formalArg1); :} ;

methodFormalList ::= LPAREN RPAREN  {: RESULT = new LinkedList<FormalArg>(); :}
                    | LPAREN formalsList:formals RPAREN {: RESULT = formals; :}  ;


statement	::= IF LPAREN expr:e1 RPAREN statement:ifStatement ELSE statement:elseStatement   {: RESULT = new IfStatement(e1, ifStatement, elseStatement); :}
                | Identifier:id1 ASSIGN expr:e1 END_SENT    {: RESULT = new AssignStatement(id1, e1); :}
                | WHILE LPAREN expr:cond RPAREN statement:blockStatement
					 {: RESULT = new WhileStatement(cond,blockStatement); :}
				| L_CUR_PAREN statementList:statements R_CUR_PAREN {: RESULT = new BlockStatement(statements); :}
				| PRINT LPAREN expr:e1 RPAREN END_SENT  {: RESULT = new SysoutStatement(e1); :}
				| Identifier:lv L_ARRAY expr:index R_ARRAY ASSIGN expr:rv END_SENT	{:RESULT = new AssignArrayStatement(lv,index,rv); :}
				;


statementList ::= statement:statement1 statementList:statements {: statements.addFirst(statement1); RESULT = statements; :}
                  | /* empty */                             	{: RESULT = new LinkedList<Statement>(); :} ;


expr ::= expr:e1 PLUS expr:e2 		{: RESULT = new AddExpr(e1, e2); :}
         | expr:e1 MINUS expr:e2	{: RESULT = new SubtractExpr(e1, e2); :}
         | expr:e1 MULT expr:e2		{: RESULT = new MultExpr(e1, e2); :}
         | expr:e1 LT expr:e2 		{: RESULT = new LtExpr(e1,e2); :}
         | expr:e1 AND expr:e2 		{: RESULT = new AndExpr(e1,e2); :}
         | NOT expr:e1        		{: RESULT = new NotExpr(e1); :}
         | LPAREN expr:e RPAREN		{: RESULT = e; :}
         | INTEGER:n				    {: RESULT = new IntegerLiteralExpr(n); :}
         | FALSE              {: RESULT = new FalseExpr(); :}
         | TRUE               {: RESULT = new TrueExpr(); :}
         | expr:ownerExpr DOT Identifier:methodId methodCallList:actuals   {: RESULT = new MethodCallExpr(ownerExpr, methodId, actuals); :}
         | NEW INT L_ARRAY expr:lengthExpr R_ARRAY {: RESULT = new NewIntArrayExpr(lengthExpr); :}
         | NEW Identifier:e LPAREN RPAREN 		{:RESULT = new NewObjectExpr(e); :}
         | arrAccessExpr:e {: RESULT = e; :}
		 | arrLengthExpr:e {: RESULT = e; :}
		 | THIS {: RESULT = new ThisExpr(); :}
		 | Identifier:identifier  {: RESULT = new IdentifierExpr(identifier); :};

arrLengthExpr ::= expr:e DOT LENGTH
                {: RESULT = new ArrayLengthExpr(e); :} ;

arrAccessExpr ::= expr:arr L_ARRAY expr:e R_ARRAY
                {: RESULT = new ArrayAccessExpr(arr,e); :} ;

methodCallList ::= LPAREN RPAREN    {: RESULT = new LinkedList<Expr>(); :}
                | LPAREN exprList:e RPAREN    {: RESULT = e; :}  ;

varDecl ::= type:varType Identifier:varName END_SENT {: RESULT = new VarDecl(varType,varName,10); :};

varDeclsList ::=  varDeclsList:vars varDecl:varDecl1 {: vars.add(varDecl1); RESULT = vars; :}
                                		 | /* empty */ 	{: RESULT = new LinkedList<VarDecl>(); :} ;

exprList ::= expr:e
            {: RESULT = new LinkedList<Expr>(); RESULT.add(e); :}
        | expr:e COMMA exprList:el
            {: el.addFirst(e); RESULT = el; :};

Identifier      ::= IDENTIFIER:id
                {: RESULT = id; :};